/*-
 * Copyright (c) 2011-2014 Robert N. M. Watson
 * All rights reserved.
 *
 * This software was developed by SRI International and the University of
 * Cambridge Computer Laboratory under DARPA/AFRL contract (FA8750-10-C-0237)
 * ("CTSRD"), as part of the DARPA CRASH research programme.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 *
 * $FreeBSD$
 */

INCLUDE ../common/common.ldscript

/*
 * When boot2 is loaded via JTAG, it's dropped at 0x10000, and will not need
 * to self-relocate, since it will be in DRAM.
 */
__boot2_base__ = 0x100000;
__boot2_base_vaddr__ = __mips64_xkphys_cached__ + __boot2_base__;

/*
 * XXXRW: Currently, miniboot interprets the ELF header rather than jumping
 * straight into the loader.  For now, give the location where we know it will
 * be.
 */
ENTRY(start)
SECTIONS
{
	. = __boot2_base_vaddr__;
	. += SIZEOF_HEADERS;
	.text ALIGN(0x10): {
		start.o(.text)
		*(EXCLUDE_FILE (start.o) .text)
	}
	.data ALIGN(0x10): { *(.data)}
	.bss ALIGN(0x10): { *(.bss) }

	__heap = ALIGN(0x8);	/* 64-bit aligned heap pointer */
	__data_end = .;
	__boot_loader_len__ = . - __boot2_base_vaddr__;
	__bss_start = ADDR(.bss);
	__bss_end = ALIGN(__bss_start + SIZEOF(.bss), 0x8);
}
