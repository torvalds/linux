/*	$OpenBSD: ld.script,v 1.2 2021/05/10 17:29:41 gkoehler Exp $	*/

/*
 * Copyright (c) 2013 Mark Kettenis <kettenis@openbsd.org>
 *
 * Permission to use, copy, modify, and distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

ENTRY(start)

PHDRS
{
	text PT_LOAD;
	openbsd_randomize PT_OPENBSD_RANDOMIZE;
}

SECTIONS
{
	. = 0x00100114;
	.text :
	{
		*(.text)
	} :text
	PROVIDE (etext = .);
	PROVIDE (_etext = .);

	.rodata :
	{
		*(.rodata .rodata.*)
	} :text

	.data.rel.ro :
	{
		*(.data.rel.ro)
	} :text

	.openbsd.randomdata :
	{
		*(.openbsd.randomdata .openbsd.randomdata.*)
	} :openbsd_randomize :text

	.data :
	{
		*(.data)
	} :text

	.sbss :
	{
		*(.sbss)
	}

	.bss :
	{
		*(.bss)
	}
	PROVIDE (end = .);
	PROVIDE (_end = .);
}
