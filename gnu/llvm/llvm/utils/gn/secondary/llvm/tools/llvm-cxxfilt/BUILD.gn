import("//llvm/tools/binutils_symlinks.gni")
import("//llvm/utils/TableGen/tablegen.gni")
import("//llvm/utils/gn/build/driver_executable.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-cxxfilt" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("cxxfilt") {  # Can't have '+' in target name.
    deps = [ ":llvm-cxxfilt" ]
    source = "llvm-cxxfilt"
    output = "$root_out_dir/bin/c++filt"  # Note: c++filt, not cxxfilt
  }
}

# //:llvm-cxxfilt depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-cxxfilt" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":cxxfilt" ]
  }
}

driver_executable("llvm-cxxfilt") {
  deps = [
    ":Opts",
    "//llvm/lib/Demangle",
    "//llvm/lib/Option",
    "//llvm/lib/Support",
    "//llvm/lib/TargetParser",
  ]
  sources = [ "llvm-cxxfilt.cpp" ]
}
