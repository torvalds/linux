import("//llvm/tools/cctools_symlinks.gni")
import("//llvm/utils/gn/build/driver_executable.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")
import("//llvm/utils/TableGen/tablegen.gni")

tablegen("Opts") {
  visibility = [ ":llvm-libtool-darwin" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_cctools_symlinks) {
  symlink_or_copy("libtool") {
    deps = [ ":llvm-libtool-darwin" ]
    source = "llvm-libtool-darwin"
    output = "$root_out_dir/bin/libtool"
  }
}

# //:llvm-libtool-darwin depends on this symlink target, see comment in
# //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-libtool-darwin" ]
  if (llvm_install_cctools_symlinks) {
    deps += [ ":libtool" ]
  }
}

driver_executable("llvm-libtool-darwin") {
  deps = [
    ":Opts",
    "//llvm/lib/Object",
    "//llvm/lib/Option",
    "//llvm/lib/Support",
    "//llvm/lib/Target:TargetsToBuild",
    "//llvm/lib/TargetParser",
  ]
  sources = [ "llvm-libtool-darwin.cpp" ]
}
