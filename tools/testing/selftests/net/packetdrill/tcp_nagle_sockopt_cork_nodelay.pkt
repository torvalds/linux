// SPDX-License-Identifier: GPL-2.0
// Test TCP_CORK and TCP_NODELAY sockopt behavior
`./defaults.sh`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < S 0:0(0) win 32792 <mss 1000,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,wscale 8>
 +.01 < . 1:1(0) ack 1 win 257
   +0 accept(3, ..., ...) = 4
// Set TCP_CORK sockopt to hold small packets
   +0 setsockopt(4, SOL_TCP, TCP_CORK, [1], 4) = 0

   +0 write(4, ..., 40) = 40
 +.05 write(4, ..., 40) = 40

// Unset TCP_CORK should push pending bytes out
 +.01 setsockopt(4, SOL_TCP, TCP_CORK, [0], 4) = 0
   +0 > P. 1:81(80) ack 1
 +.01 < . 1:1(0) ack 81 win 257

// Set TCP_CORK sockopt to hold small packets
   +0 setsockopt(4, SOL_TCP, TCP_CORK, [1], 4) = 0

   +0 write(4, ..., 40) = 40
 +.05 write(4, ..., 40) = 40

// Set TCP_NODELAY sockopt should push pending bytes out
   +0 setsockopt(4, SOL_TCP, TCP_NODELAY, [1], 4) = 0
   +0 > P. 81:161(80) ack 1
 +.01 < . 1:1(0) ack 161 win 257

// Set MSG_MORE to hold small packets
   +0 send(4, ..., 40, MSG_MORE) = 40
 +.05 send(4, ..., 40, MSG_MORE) = 40

// Set TCP_NODELAY sockopt should push pending bytes out
 +.01 setsockopt(4, SOL_TCP, TCP_NODELAY, [1], 4) = 0
   +0 > . 161:241(80) ack 1
 +.01 < . 1:1(0) ack 241 win 257
