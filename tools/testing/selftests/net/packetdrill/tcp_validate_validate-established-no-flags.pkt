// SPDX-License-Identifier: GPL-2.0
// Verify that established connections drop a segment without the ACK flag set.

`./defaults.sh`

// Create a socket.
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

// Establish a connection.
   +0 < S 0:0(0) win 20000 <mss 1000,sackOK,nop,nop>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,nop,sackOK>
 +.01 < . 1:1(0) ack 1 win 20000
   +0 accept(3, ..., ...) = 4

// Receive a segment with no flags set, verify that it's not enqueued.
 +.01 < - 1:1001(1000) win 20000
   +0 ioctl(4, SIOCINQ, [0]) = 0

// Receive a segment with ACK flag set, verify that it is enqueued.
 +.01 < . 1:1001(1000) ack 1 win 20000
   +0 ioctl(4, SIOCINQ, [1000]) = 0
