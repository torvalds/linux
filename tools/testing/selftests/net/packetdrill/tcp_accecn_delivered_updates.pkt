// Test basic AccECN CEP/CEB/E0B/E1B functionality & CEP wrapping

`./defaults.sh
sysctl -q net.ipv4.tcp_ecn=3
`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < SEWA 0:0(0) win 32792 <mss 1050,nop,nop,sackOK,nop,wscale 8>
+.002 > SW. 0:0(0) ack 1 <mss 1460,ECN e1b 1 ceb 0 e0b 1,nop,nop,nop,sackOK,nop,wscale 8>
+0.05 < [ect0] W. 1:1(0) ack 1 win 264 <ECN e0b 1 ceb 0 e1b 1,nop>
+.002 accept(3, ..., ...) = 4

+0.01 %{
assert tcpi_delivered_ce == 0, tcpi_delivered_ce
assert tcpi_delivered_ce_bytes == 0, tcpi_delivered_ce_bytes
}%

+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] EAP. 1:1001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
  // Fake CE
+0.05 < [ect0] WA. 1:1(0) ack 1001 win 264 <ECN e0b 1 ceb 1000 e1b 1,nop>

+0.01 %{
assert tcpi_delivered_ce == 1, tcpi_delivered_ce
assert tcpi_delivered_ce_bytes == 1000, tcpi_delivered_ce_bytes
}%

+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] EAP. 1001:2001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
  // Fake ect0
+0.05 < [ect0] WA. 1:1(0) ack 2001 win 264 <ECN e0b 1001 ceb 1000 e1b 1,nop>

+0.01 %{
assert tcpi_delivered_ce == 1, tcpi_delivered_ce
assert tcpi_delivered_e0_bytes == 1000, tcpi_delivered_e0_bytes
}%

+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] EAP. 2001:3001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
  // Fake ce
+0.05 < [ect0] EWA. 1:1(0) ack 3001 win 264 <ECN e0b 1001 ceb 2000 e1b 1,nop>

+0.01 %{
assert tcpi_delivered_ce == 2, tcpi_delivered_ce
assert tcpi_delivered_ce_bytes == 2000, tcpi_delivered_ce_bytes
}%

+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] EAP. 3001:4001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
  // Fake ect1
+0.05 < [ect0] EWA. 1:1(0) ack 4001 win 264 <ECN e0b 1001 ceb 2000 e1b 1001,nop>

+0.01 %{
assert tcpi_delivered_ce == 2, tcpi_delivered_ce
assert tcpi_delivered_e1_bytes == 1000, tcpi_delivered_e1_bytes
}%

+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] EAP. 4001:5001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
  // Fake ce
+0.05 < [ect0] . 1:1(0) ack 5001 win 264 <ECN e0b 1001 ceb 3000 e1b 1001,nop>

+0.01 %{
assert tcpi_delivered_ce == 3, tcpi_delivered_ce
assert tcpi_delivered_ce_bytes == 3000, tcpi_delivered_ce_bytes
}%
