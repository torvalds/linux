// SPDX-License-Identifier: GPL-2.0
//
// Minimal passive open.
// Peer is first to close.

`./defaults.sh`

   // Open listener socket
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   // Incoming connection: passive open: three-way handshake
   +0 < S 0:0(0) win 65535 <mss 1000,sackOK,nop,nop,nop,wscale 8>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,nop,sackOK,nop,wscale 8>
   +0 < . 1:1(0) ack 1 win 257

   // Open connection socket and close listener socket
   +0 accept(3, ..., ...) = 4
   +0 close(3) = 0

   // Peer sends data: acknowledge and receive
   +0 < P. 1:1001(1000) ack 1 win 257
   +0 > . 1:1(0) ack 1001
   +0 recv(4, ..., 1000, 0) = 1000

   // Peer initiates connection close
   +0 < F. 1001:1001(0) ack 1 win 257
 +.04 > . 1:1(0) ack 1002

   // Local socket also closes its side
   +0 close(4) = 0
   +0 > F. 1:1(0) ack 1002
   +0 < . 1002:1002(0) ack 2 win 257
